## For Loops

There are 3 variants of `for loops` in LiteScript

-------------------------------------------------------
### Variant 1) 'for each index,value in array' 
to loop over *Array indexes and items* ###

Examples:

    var months = [ 'Jan', 'Feb', 'March' ]

    for each month in months
        print month

> Jan Feb March

    for each index,month in months
        print index,month

> 0 Jan 1 Feb 2 March

You can also use `where` to filter 

    for each index,month in months where month isnt 'Feb'
        print index,month

> 0 Jan 2 March


-------------------------------------------------------
### Variant 2) 'for each property' 
to loop over *object property names and values* ###

Examples:

    function showAllProps ( object )
    
      for each property name,value in object
          print "property '#{name}' is #{value}"

    function showProps ( object )
    
      print "Object own properties:"
      for each own property propName,propValue in object
          print '#{propName}:#{propValue}'

    function showStringOptions ( options )
    
      for each own property key,value in options 
        where type of value is 'string'
          print "'#{key}' has a string value"


-------------------------------------------------------
##### Variant 3) 'for index=...' to create *numeric loops* 

This `for` variant is just a verbose expression 
of the standard C (and js) `for()` loop.
the loop variable increment is included by default.

Examples:

    for i=0 while i<3
      print months[i]

    // print event numbers until 10, except 6
    for i=0 until i is 10, where i isnt 6, i+=2
      print i

    // print chars 5 to 9, reversed
    var s = "a string"
    for p=9, while p>4, p--
      print s[p]

